import java.nio.file.Paths

buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
    }
}

apply plugin: "net.minecraftforge.gradle.forge"

repositories {
    maven { // TOP
        name 'tterrag maven'
        url "http://maven.tterrag.com/"
    }
    maven {
        name = "CoFH Maven"
        url = "http://maven.covers1624.net"
    }
}

ext.build_rev = 0
if(System.getenv("BUILD_REV") != null) {
    project.build_rev = System.getenv("BUILD_REV")
}

version = "${minecraft_version}-${mod_version}.${build_rev}"
group = "dyn.healingbrew.polarbearexpress"
archivesBaseName = "polarbearexpress"

sourceCompatibility = targetCompatibility = "1.8"
compileJava {
    sourceCompatibility = targetCompatibility = "1.8"
}

minecraft {
    version = "${minecraft_version}-${forge_version}"
    runDir = "run"
    mappings = "snapshot_20171003"

    replace "@VERSION@", project.mod_version
    replaceIn "PolarBearExpress.java"
}

task signJar(type: SignJar, dependsOn: reobfJar) {
    inputFile = jar.archivePath
    outputFile = jar.archivePath

    keyStore = System.getenv("FMLSIGN_KEYSTORE")
    alias = "signFiles"
    storePass = System.getenv("FMLSIGN_STOREPASS")
    keyPass = System.getenv("FMLSIGN_KEYPASS")
}

if(System.getenv("FMLSIGN_KEYSTORE")  != null && 
   System.getenv("FMLSIGN_STOREPASS") != null && 
   System.getenv("FMLSIGN_KEYPASS")   != null) {
    build.dependsOn signJar
    System.out.println("Enabling signing")
}

runClient {
    jvmArgs "-Dfml.coreMods.load=dyn.healingbrew.polarbearexpress.core.HBPlugin"
}

jar {
    manifest {
        attributes "FMLCorePlugin": "dyn.healingbrew.polarbearexpress.core.HBPlugin"
        attributes "FMLCorePluginContainsFMLMod": true
    }
}

dependencies {
    deobfCompile "cofh:RedstoneFlux:1.12-2.0.0.1:universal"
    deobfCompile "mcjty.theoneprobe:TheOneProbe-1.12:1.12-1.4.19-11:api"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"
		
        expand "version":project.version, "mcversion":project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
}
